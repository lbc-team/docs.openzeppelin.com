= 开发智能合约

欢迎来到激动人心的智能合约开发世界！这个指南将会带领你开始使用Solidity编写智能合约，接下来你将会了解以下内容：

 * <<setting-up-a-solidity-project, 建立一个智能合约项目>>
 * <<compiling-solidity-source-code, 编译Solidity源代码>>
 * <<adding-more-contracts, 添加更多合约>>
 * <<using-openzeppelin-contracts, 使用OpenZeppelin合约库>>

=== 关于Solidity

在本指南中，我们不会介绍Solidity语言的概念，例如语法或关键字。你可以通过查看以下内容来了解Solidity语言，这些内容对新手和有经验的开发人员来说都是很好的学习资源：

 * 想要对以太坊和智能合约有一个总体的了解, 在以太坊的官方网站 https://ethereum.org/learn/[学习Ethereum] 中有很多对新手友好的内容.
 * 如果你刚接触Solidity,  https://solidity.readthedocs.io/en/latest/introduction-to-smart-contracts.html[Solidity官方文档] 是一个好用的资源. 阅读他们的 https://solidity.readthedocs.io/en/latest/security-considerations.html[安全考量], 就可以很好地了解区块链和传统软件平台之间的差异.
 * https://consensys.github.io/smart-contract-best-practices/[最佳实践] 的内容非常广泛, 可以学习到 https://consensys.github.io/smart-contract-best-practices/recommendations/[经过验证的模式] 和 https://consensys.github.io/smart-contract-best-practices/known_attacks/[已知陷阱] .
 * https://solidity-05.ethernaut.openzeppelin.com/[Ethernaut] 是一个基于网页的游戏，你可以在里面寻找智能合约中的漏洞，游戏的难度也会随之增加。

了解了这些，下面我们就开始吧！

[[setting-up-a-solidity-project]]
== 建立一个智能合约项目

建立项目的第一步就是创建一个 `contracts` 目录, 在目录中存放我们的Solidity源代码 (`.sol`)。就像是使用其他你可能熟悉的语言时的 `src` 目录一样。 

我们就可以编写我们的第一个简单的智能合约了，命名为 `Box` ：这个合约可以允许用户存入一个值并且可以取出。

我们将这个合约保存为 `contracts/Box.sol` 。每一个 `.sol` 文件都应该有独立合约的代码并且以它命名。

```solidity
// contracts/Box.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;


contract Box {
    uint256 private value;

    // 当存储的value变化时弹出
    event ValueChanged(uint256 newValue);

    // 在合约中存入新的value
    function store(uint256 newValue) public {
        value = newValue;
        emit ValueChanged(newValue);
    }

    // 读取最新的value
    function retrieve() public view returns (uint256) {
        return value;
    }
}
```

[[compiling-solidity-source-code]]
== 编译Solidity源代码

以太坊虚拟机 (EVM) 可以运行Solidity代码：我们首先需要将它编译为EVM字节码。

我们将会使用到 xref:cli::index.adoc[*OpenZeppelin 命令行接口 (CLI)*]:

```console
$ npm install --save-dev @openzeppelin/cli
```

CLI功能非常强大，并拥有很多令人激动的功能（例如升级智能合约和交互式交易）。我们会经常使用到它。

现在还是让我们专注于编译吧。编译可以通过运行单个命令来实现：

```console
$ npx oz compile
```

NOTE: 如果你不熟悉 `npx` 命令, 查看 xref:setting-up-a-node-project.adoc#using-npx[建立Node项目指南].

`compile` 命令将会自动寻找 `contracts` 目录下的所有合约，并用适合合约的Solidity版本编译器编译它们。

你将会发现新建了 `build/contracts` 目录：里面保存了编译后的文件（字节码和数据），都是 `.json` 格式的文件。最好将这个目录增加到你的 `.gitignore` 中。

你还可以通过将参数传递给 `compile` 命令来配置编译，包括选择编译器版本和启用优化：

```console
$ npx oz compile --solc-version=0.6.7 --optimizer on
```

如果想要了解这些选项的细节，可以参考 xref:cli::compiling.adoc[使用CLI进行编译]。

[[adding-more-contracts]]
== 添加更多合约

随着项目的发展，你将创建更多相互交互的合约：每个合约都应存储在自己的.sol文件中。

要理解它是什么样的，让我们在 `Box` 合约中添加一个简单的访问控制系统：我们将在合约中存储管理员地址 `Auth` ，并且仅允许 `Box` 被那些经过 `Auth` 允许使用的帐户使用。
 
由于编译器将识别 `contracts` 目录和子目录中的所有文件，因此你可以随意地组织自己认为合适的代码。在这里，我们将 `Auth` 合约存储在一个 `access-control` 子目录中：

[[auth-contract]]
```solidity
// contracts/access-control/Auth.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;


contract Auth {
    address private administrator;

    constructor() public {
        // 使合约的发布者成为合约的管理员
        administrator = msg.sender;
    }

    function isAdministrator(address user) public view returns (bool) {
        return user == administrator;
    }
}
```
为了在 `Box` 合约中使用它，我们使用import语句引用 `Auth` 的相对路径：

```solidity
// contracts/Box.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

// 从access-control子目录引入Auth
import "./access-control/Auth.sol";


contract Box {
    uint256 private value;
    Auth private auth;

    event ValueChanged(uint256 newValue);

    constructor(Auth _auth) public {
        auth = _auth;
    }

    function store(uint256 newValue) public {
        // 要求调用者时Auth合约的管理员
        require(auth.isAdministrator(msg.sender), "Unauthorized");

        value = newValue;
        emit ValueChanged(newValue);
    }

    function retrieve() public view returns (uint256) {
        return value;
    }
}
```
让合约保持简单的一个很好的方式就是将关键功能分到多个合约中，这是一个好习惯。

但是这并不是将代码模块化的唯一方式。你也可以在Solidity中使用继承进行代码封装和重用，我们接下来将会学到这些。

[[using-openzeppelin-contracts]]
== 使用OpenZeppelin合约库

可重用的模型和库是伟大软件的基础。xref:contracts::index.adoc[*OpenZeppelin合约*] 库包含了大量对智能合约开发有用的模块。你可以放心的使用它们：它们已经过多次审核，并且经过了安全性和正确性的测试。

=== 关于继承

库中的许多合约都不是独立的，也就是说，你不能直接部署它们。而应该是通过向其添加功能来丰富自己的合约。Solidity提供了多重继承的机制来解决这个问题：可以查看 https://solidity.readthedocs.io/en/latest/contracts.html#inheritance[官方文档] 来了解其中的细节。

例如， xref:contracts:api:ownership.adoc#Ownable[`Ownable`] 合约将部署者的帐户标记为合约的所有者，并提供了一个名为 `onlyOwner` 的修饰符。当应用到某个函数时，onlyOwner将回退所有非所有者账户的函数调用。还提供 xref:contracts:api:ownership.adoc#Ownable-transferOwnership-address-[转账] 和 xref:contracts:api:ownership.adoc#Ownable-renounceOwnership--[放弃] 所有权的功能。

使用这种方法，继承将成为一种强大的模块化机制，你可以不用被迫部署和管理多个合约。

=== 引入OpenZeppelin合约

可以通过运行以下命令来下载最新版本的OpenZeppelin合约库：

```console
$ npm install --save-dev @openzeppelin/contracts
```

NOTE: 你应当使用已经发布的库：在项目中直接复制粘贴库中的源代码是一个危险的操作，有可能会给你的合约引入安全漏洞。

想要使用OpenZeppelin的合约时，你需要使用 `import` 命令并加上 `@openzeppelin/contracts` 前缀。例如，为了代替我们的 <<auth-contract, `Auth`>> 合约，我们需要引入 `@openzeppelin/contracts/access/Ownable.sol` 来为 `Box` 增加访问控制：

[[box-contract]]
```solidity
// contracts/Box.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

// 从OpenZeppelin合约库引入Ownable合约
import "@openzeppelin/contracts/access/Ownable.sol";


// 使Box继承Ownable合约
contract Box is Ownable {
    uint256 private value;

    event ValueChanged(uint256 newValue);

    // onlyOwner修饰符限制了调用store的账户
    function store(uint256 newValue) public onlyOwner {
        value = newValue;
        emit ValueChanged(newValue);
    }

    function retrieve() public view returns (uint256) {
        return value;
    }
}
```

xref:contracts::index.adoc[OpenZeppelin合约官方文档] 是一个学习开发安全智能合约系统的优秀指南。它包含了指导和API依赖的细节：你可以通过阅读 xref:contracts::access-control.adoc[访问控制] 这个例子来了解更多关于刚才使用到 `Ownable` 合约的细节。

== 下一步

想要你的去中心化应用运行在以太坊上，编写和编译智能合约是第一步。如果你喜欢这个开始，那么就继续进一步的任务：

 * xref:deploying-and-interacting.adoc[发布和交互]
 * xref:writing-automated-tests.adoc[编写自动化测试]
 * xref:connecting-to-public-test-networks.adoc[连接公网]
